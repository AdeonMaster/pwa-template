import groovy.json.JsonSlurperClassic 

@NonCPS
def jsonParse(def json) {
    new groovy.json.JsonSlurperClassic().parseText(json)
}

def getGitBranches(accessToken, owner, repo) {
    return jsonParse(sh(script: "curl -H 'Authorization: token ${accessToken}' https://api.github.com/repos/${owner}/${repo}/branches", returnStdout: true))
}

def getGitReleases(accessToken, owner, repo) {
    return jsonParse(sh(script: "curl -H 'Authorization: token ${accessToken}' https://api.github.com/repos/${owner}/${repo}/releases", returnStdout: true))
}

def createGitRelease(accessToken, owner, repo, tagName, targetCommitish, name, body, draft = false, prerelease = false) {
    return jsonParse(sh(script: "curl -H 'Authorization: token ${accessToken}' -d '{ \"tag_name\": \"${tagName}\", \"target_commitish\": \"${targetCommitish}\", \"name\": \"${name}\", \"body\": \"${body}\", \"draft\": ${draft}, \"prerelease\": ${prerelease} }' https://api.github.com/repos/${owner}/${repo}/releases", returnStdout: true))
}

def uploadGitReleaseAsset(accessToken, owner, repo, releaseId, fileName) {
    return jsonParse(sh(script: "curl -H 'Authorization: token ${accessToken}' -H 'Content-Type: application/octet-stream' --data-binary @'${fileName}' https://uploads.github.com/repos/${owner}/${repo}/releases/${releaseId}/assets?name=${fileName}", returnStdout: true))
}

def getPackageVersion() {
    return sh(script: "node -p \"require('./package.json').version\"", returnStdout: true).replaceAll('\\s', '')
}

pipeline {
    agent any
    
    tools {
        nodejs 'nodejs-15.5.0'
    }
    
    environment {
        GIT_ACCESS_TOKEN = 'YOUR_TOKEN_HERE'
        GIT_OWNER = 'AdeonMaster'
        GIT_REPO = 'pwa-template'
    }
  
    stages {
        stage('Input') {
            steps {
                script {
                    def branches = getGitBranches(env.GIT_ACCESS_TOKEN, env.GIT_OWNER, env.GIT_REPO)
                    
                    // there is an error
                    if (branches instanceof HashMap) {
                        currentBuild.result = 'ABORTED'
                        error('Error while receiving branch list. '+branches.message)
                    }
                    
                    env.TARGET_BRANCH = input(
                        message: 'Please, select target branch to make a release from',
                        ok: 'Select',
                        parameters: [
                            choice(name: 'Branch', choices: (branches.collect {branch -> return branch.name}))
                        ]
                    )
                    env.TARGET_VERSION = input(
                        message: 'Please, select target version',
                        ok: 'Select',
                        parameters: [
                            choice(name: 'Version', choices: ['patch', 'minor', 'major', 'custom'].join('\n'))
                        ]
                    )
                    
                    if (env.TARGET_VERSION == 'custom') {
                        env.TARGET_VERSION = input(
                            message: 'Please, enter a custom version\n(ex: 1.0.0-patch-RC1)', 
                            parameters: [
                                [$class: 'TextParameterDefinition', name: 'Version']
                            ]
                        )
                    }
                    
                    def releases = getGitReleases(env.GIT_ACCESS_TOKEN, env.GIT_OWNER, env.GIT_REPO)
                    if (releases instanceof HashMap) {
                        currentBuild.result = 'ABORTED'
                        error('Error while receiving releases list. '+releases.message)
                    }
                    
                    env.IS_FIRST_RELEASE = !releases.size()
                    
                    echo 'Variables:'
                    echo "${env.TARGET_BRANCH}"
                    echo "${env.TARGET_VERSION}"
                    echo "${env.IS_FIRST_RELEASE}"
                }
            }
        }
       
        stage('Git checkout') {
            steps {
                sh 'rm -rf *'
                sh 'git clone git@github.com:${GIT_OWNER}/${GIT_REPO}.git'
                dir("${GIT_REPO}") {
                    sh 'git checkout ${TARGET_BRANCH}'
                }
            }
        }
        
        stage('Install') {
            steps {
                dir("${GIT_REPO}") {
                    sh '''
                        if [ -f "package-lock.json" ]; then
                            npm ci
                        else 
                            npm install
                        fi
                    '''
                }
            }
        }  
        
        stage('Run tests') {
            steps {
                dir("${GIT_REPO}") {
                    sh 'npm run eslint'
                    sh 'npm run jest'
                }
            }
        }

        stage('Update version') {
            steps {
                dir("${GIT_REPO}") {
                    script {
                        def currentPackageVersion = getPackageVersion()
                        echo 'Current package version: '+currentPackageVersion

                        // check if patch/minor/major is applied after non standard custom
                        if (['patch', 'minor', 'major'].contains(env.TARGET_VERSION) && !(currentPackageVersion =~ /^\d+.\d+.\d+$/).size()) {
                            currentBuild.result = 'ABORTED'
                            error('You\'re trying to increase version number of a non standard version pattern. Please use a custom version with standard pattern eq. 1.0.0')
                        }
                        
                        sh(script: "npm version ${TARGET_VERSION}")
                    }
                }
            }
        }
      
        stage('Build') {
            steps {
                dir("${GIT_REPO}") {
                    sh 'npm run build'
                }
            }
        }

        stage('Release') {
            steps {
                dir("${GIT_REPO}") {
                    sh 'git push --set-upstream origin ${TARGET_BRANCH} --tags'
                    sh 'git push --set-upstream origin ${TARGET_BRANCH}'
                    sh 'zip -r dist.zip dist/'
                    script {
                        def version = getPackageVersion()
                        def release = createGitRelease(env.GIT_ACCESS_TOKEN, env.GIT_OWNER, env.GIT_REPO, version, env.TARGET_BRANCH, version, "Release of ${version}")

                        uploadGitReleaseAsset(env.GIT_ACCESS_TOKEN, env.GIT_OWNER, env.GIT_REPO, release.id, 'dist.zip')
                    }
                }
            }
        }
    }
}